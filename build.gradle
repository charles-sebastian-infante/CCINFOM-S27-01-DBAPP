plugins {
    id 'java'
    id 'war'
}

group = 'com.busterminal'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    // MySQL Connector
    implementation 'mysql:mysql-connector-java:8.0.33'
    
    // Servlet API (provided by Tomcat)
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'javax.servlet.jsp:javax.servlet.jsp-api:2.3.1'
    compileOnly 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.1'
    
    // Testing (optional)
    testImplementation 'junit:junit:4.13.2'
}

// Configure WAR packaging
war {
    archiveFileName = 'bus_terminal_app.war'
}

// Compile Java task
tasks.named('compileJava') {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

// Custom task to display classpath (for debugging)
task showClasspath {
    doLast {
        println "Classpath:"
        configurations.runtimeClasspath.each { println "  - " + it }
    }
}

// Custom task to clean build
task cleanBuild {
    doLast {
        delete 'build'
        println "Build directory cleaned"
    }
}

task deploy {
    dependsOn 'build'
    doLast {
        String catalinaHome = System.getenv('CATALINA_HOME')
        String os = System.getProperty('os.name').toLowerCase()
        
        if (!catalinaHome) {
            throw new GradleException('CATALINA_HOME not set!')
        }
        
        println "(^_^) Deploying to Tomcat 9"
        
        // Stop Tomcat first
        println "(´；ω；`) Stopping Tomcat..."
        if (os.contains('win')) {
            "cmd /c ${catalinaHome}\\bin\\catalina.bat stop".execute().waitFor()
        } else {
            "bash ${catalinaHome}/bin/catalina.sh stop".execute().waitFor()
        }
        Thread.sleep(2000)
        
        // Clean old deployment
        println "(´・ω・`) Cleaning old deployment..."
        new File("$catalinaHome/webapps/bus_terminal_app").deleteDir()
        new File("$catalinaHome/webapps/bus_terminal_app.war").delete()
        Thread.sleep(1000)
        
        // Copy WAR
        println "(´・ω・`) Deploying new WAR..."
        def warFile = new File("build/libs/bus_terminal_app.war")
        warFile.renameTo(new File("$catalinaHome/webapps/bus_terminal_app.war"))
        
        // Start Tomcat
        println "(´▽`ノノ Starting Tomcat..."
        if (os.contains('win')) {
            "cmd /c ${catalinaHome}\\bin\\catalina.bat start".execute()
        } else {
            "bash ${catalinaHome}/bin/catalina.sh start".execute()
        }
        Thread.sleep(3000)
        
        println "(๑•́ ω •̀)و Done!"
    }
}
